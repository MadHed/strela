// Copyright (c) 2018 Stephan Unverwerth
// This code is licensed under MIT license (See LICENSE for details)

module Examples.Ray {
    import Std.IO.*;
    import Std.Collections.List;
    import Std.Math.Lina.*;
    import Std.cstdio.*;
    import Std.Math.*;

    class TGA {
        var file: File;
        
        function init(name: String, width: int, height: int) {
            file = new File(name, "wb");
            file.write(0: u8); // no id
            file.write(0: u8); // no palette
            file.write(2: u8); // 24 bit rgb
            file.write(0: u16); // palette begin
            file.write(0: u16); // palette length
            file.write(0: u8); // palette entry size
            file.write(0: u16); // x begin
            file.write(0: u16); // y begin
            file.write(width: u16); // width
            file.write(height: u16); // height
            file.write(24: u8); // bpp
            file.write(32: u8); // attrib bits (32 = top to bottom)
        }

        function pixel(color: Vec) {
            var r: u8;
            var g: u8;
            var b: u8;

            if (color.x > 1.0) r = 255;
            else if (color.x < 0.0) r = 0;
            else r = color.x * 255.0;

            if (color.y > 1.0) g = 255;
            else if (color.y < 0.0) g = 0;
            else g = color.y * 255;

            if (color.z > 1.0) b = 255;
            else if (color.z < 0.0) b = 0;
            else b = color.z * 255;

            file.write(b);
            file.write(g);
            file.write(r);
        }
    }

    class Material {
        var color: Vec;
        var roughness: f64;
        var reflectivity: f64;

        function init(color: Vec, roughness: f64, reflectivity: f64) {
            this.color = color;
            this.roughness = roughness;
            this.reflectivity = reflectivity;
        }
    }

    class Plane {
        var position: Vec;
        var normal: Vec;
        var material: Material;

        function init(position: Vec, normal: Vec, material: Material) {
            this.position = position;
            this.normal = normal;
            this.material = material;
        }

        function trace(ray: Ray, hit: Hit) {
            var pn = normal;

            var d = (position - ray.origin).dot(pn) / ray.direction.dot(pn);
            if (d > 0.0 && d < hit.distance) {
                hit.distance = d;
                hit.position = ray.origin + ray.direction * d;
                hit.normal = pn;
                hit.material = material;
            }
        }

        function isBehind(p: Vec): bool {
            return (position - p).dot(normal) > 0.0;
        }
    }

    class Sphere {
        var center: Vec;
        var radius: f64;
        var material: Material;

        function init(center: Vec, radius: f64, material: Material) {
            this.center = center;
            this.radius = radius;
            this.material = material;
        }

        function trace(ray: Ray, hit: Hit) {
            var oc = ray.origin - center;

            var a = ray.direction.dot(oc);
            a = a * a - oc.dot(oc) + radius * radius;

            if (a > 0.0) {
                var d = -ray.direction.dot(oc) - sqrt(a);

                if (d > 0.0 && d < hit.distance) {
                    hit.distance = d;
                    hit.position = ray.origin + ray.direction * d;
                    hit.normal = (hit.position - center) / radius;
                    hit.material = material;
                }
            }
        }
    }

    class Polyhedron {
        var planes: List<Plane>;
        var material: Material;

        function init() {
            planes = new List<Plane>;
        }

        function trace(ray: Ray, hit: Hit) {
            var i = 0;
            var myhit = new Hit;
            myhit.distance = hit.distance;
            while (i < planes.length) {
                planes[i].trace(ray, myhit);
                if (myhit.distance < hit.distance) {
                    var count = 1;
                    var j = 0;
                    while (j < planes.length) {
                        if (j != i && planes[j].isBehind(myhit.position)) {
                            count++;
                        }
                        j++;
                    }
                    if (count == planes.length) {
                        hit.distance = myhit.distance;
                        hit.position = myhit.position;
                        hit.normal = myhit.normal;
                        hit.material = myhit.material;
                        return;
                    }
                }
                i++;
            }
        }
    }

    class Ray {
        var origin: Vec;
        var direction: Vec;

        function init(origin: Vec, direction: Vec) {
            this.origin = origin;
            this.direction = direction;
        }
    }

    class Hit {
        var position: Vec;
        var normal: Vec;
        var material: Material;
        var distance: f64;
    }

    class Tracer {
        var spheres: List<Sphere>;
        var planes: List<Plane>;
        var polyhedrons: List<Polyhedron>;
        var file: TGA;
        var width: int;
        var height: int;

        function init() {
            polyhedrons = new List<Polyhedron>;

            planes = new List<Plane>();
            planes.append(new Plane(new Vec(-20, 0, 20), (new Vec(1, 0, -1)).normalized(), new Material(new Vec(0, 0, 0), 0.05, 0.75) ));

            spheres = new List<Sphere>();
            spheres.append(new Sphere(new Vec(5, 5, 5), 5, new Material(new Vec(0, 0, 0), 0.0, 1.0)));
            spheres.append(new Sphere(new Vec(5, -5, 10), 5, new Material(new Vec(1, 0, 0), 0.0, 0.8)));
            spheres.append(new Sphere(new Vec(-6, 6, 15), 5, new Material(new Vec(0, 1, 0), 0.1, 0.6)));
            spheres.append(new Sphere(new Vec(-6, -5, 20), 5, new Material(new Vec(0, 0, 1), 0.0, 0.4)));
            
            width = 64;
            height = 64;
            file = new TGA("out.tga", width, height);
            print("Initialized image with ");
            print(width);
            print("x");
            print(height);
            println(" pixels.");
        }

        function trace(ray: Ray, recurse: int): Vec {
            var hit = new Hit;
            hit.distance = 100000.0;

            var i = 0;
            var numObj = spheres.length;
            while (i < numObj) {
                spheres[i].trace(ray, hit);
                i++;
            }

            i = 0;
            numObj = planes.length;
            while (i < numObj) {
                planes[i].trace(ray, hit);
                i++;
            }

            i = 0;
            numObj = polyhedrons.length;
            while (i < numObj) {
                polyhedrons[i].trace(ray, hit);
                i++;
            }

            if (hit.distance < 100000) {
                var mat = hit.material;
                if (recurse < 15 && hit.material.reflectivity > 0.0) {
                    var rough = mat.roughness;
                    var n2 = (hit.normal + new Vec(frand()*rough, frand()*rough, frand()*rough)).normalized();
                    var r = reflect(ray.direction, n2);
                    return lerp(mat.color * clamp(n2.y), trace(new Ray(hit.position + r * 0.0001, r), recurse + 1), mat.reflectivity);
                }
                else {
                    return mat.color * clamp(hit.normal.y);
                }
            }

            if (ray.direction.y < 0) {
                var v = ray.direction.y * 0.4;
                return new Vec(0.4 + v, 0.3 + v, 0.1);
            }
            else {
                var v = ray.direction.y * 0.8;
                return new Vec(0.4 + v, 0.4 + v, 0.7 + v * 0.2);
            }
        }

        function run() {
            var x: u32 = 0;
            var y: u32 = 0;
            var dir = new Vec;
            var origin = new Vec(0, 0, 0);

            var hw = width * 0.5;
            var hh = height * 0.5;

            while (y < height) {
                while (x < width) {
                    var color = new Vec(0, 0, 0);
                    
                    var num: i32 = 0;

                    var ix = -2;
                    var iy = -2;
                    while (iy < 3) {
                        while (ix < 3) {
                            dir.x = x + ix * 0.4 - hw;
                            dir.y = -(y + iy * 0.4 - hh);
                            dir.z = width * 0.3;
                            dir.normalize();
                            color = color + trace(new Ray(origin, dir), 0);
                            num++;
                            ix++;
                        }
                        ix = -1;
                        iy++;
                    }

                    file.pixel(color * (1.0 / num));
                    x++;
                }
                x = 0;
                y++;
                println(y);
            }

            println("Fin");
        }
    }

    function main(args: String[]): int {
        var tracer = new Tracer;
        tracer.run();

        return 0;
    }
}