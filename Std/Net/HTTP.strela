// Copyright (c) 2018 Stephan Unverwerth
// This code is licensed under MIT license (See LICENSE for details)

module Std.Net.HTTP {
    import Std.Net.Socket;
    import Std.Net.IPAddr;
    import Std.Collections.Map;
    import Std.IO.*;

    export class Client {
        var socket: Socket;
        
        function init() {
            this.socket = new Socket;
        }

        function connect(ip: u32, port: u16) {
            this.socket.connect(ip, port);
        }

        function connect(ip: IPAddr, port: u16) {
            this.socket.connect(ip.toInt(), port);
        }

        function request(request: Request): Response {
            this.socket.write(request.method + " " + request.path + " HTTP/1.1\r\n");
            var i: int = 0;
            while (i < request.headers.keys.length) {
                this.socket.write(request.headers.keys[i]);
                this.socket.write(": ");
                this.socket.write(request.headers.values[i]);
                this.socket.write("\r\n");

                i++;
            }
            this.socket.write("\r\n");

            var response = new Response();
            var line = this.socket.readLine();
            while (line != "\r\n" && line != "") {
                line = this.socket.readLine();
                var parts = line.split(":");
                if (parts.length == 2) {
                    response.headers.set(parts[0].trim(), parts[1].trim());
                }
            }

            line = this.socket.readLine();
            while (line != "") {
                response.body = response.body + line;
                line = this.socket.readLine();
            }
            return response;
        }
    }

    export class Server {
        var socket: Socket;
        
        function init(address: i32, port: u16) {
            this.socket = new Socket(address, port);
            this.socket.listen();
        }

        function accept(): Request {
            var sock = this.socket.accept();
            var request = new Request;
            request.socket = sock;
            
            var line = sock.readLine();
            
            var parts = line.split(" ");
            request.method = parts[0];
            request.path = parts[1];

            while (line != "\r\n") {
                line = sock.readLine();
                var parts = line.split(":", 2);
                if (parts.length == 2) {
                    request.headers.set(parts[0].trim(), parts[1].trim());
                }
            }

            return request;
        }

        function close() {
            this.socket.shutdown();
            this.socket.close();
        }
    }

    export class Request {
        var socket: Socket;
        var path: String;
        var headers: Map<String, String>;
        var body: String;
        var method: String;

        function init() {
            this.path = "";
            this.body = "";
            this.method = "GET";
            this.headers = new Map<String, String>;
        }

        function respond(response: Response) {
            this.socket.write("HTTP/1.1 " + toString(response.statusCode) + " " + response.statusMessage + "\r\n");
            var i: int = 0;
            while (i < response.headers.keys.length) {
                this.socket.write(response.headers.keys[i]);
                this.socket.write(": ");
                this.socket.write(response.headers.values[i]);
                this.socket.write("\r\n");
                i++;
            }
            this.socket.write("Connection: close\r\n");
            this.socket.write("\r\n");
            this.socket.write(response.body);
            this.socket.shutdown();
            this.socket.close();
        }
    }

    export class Response {
        var statusCode: u16;
        var statusMessage: String;
        var headers: Map<String, String>;
        var body: String;

        function init() {
            this.statusMessage = "";
            this.body = "";
            this.headers = new Map<String, String>;
        }
    }
}